# Metview Macro

# Plot_EcuadorMask_obsFF.mv plots Ecuador's mask for the ECMWF ENS grid
# together with the flash flood reports

# INPUT PARAMETERS
Year_ObsFF = 2020
ThrEFFCI = 10
Git_repo = "/vol/ecpoint/mofp/PhD/Papers2Write/FlashFloods_Ecuador"
FileIN_Emask = "Data/Processed/EcuadorMasks_ENS/Ecuador_3Regions.grib"
DirIN_ObsFF = "Data/Processed/ObsFF"
DirOUT = "Data/Figures/EcuadorMask_ObsFF"
#########################################################################


# Read Ecuador's mask
ecuador_mask = read(Git_repo & "/" & FileIN_Emask)


# Read flash flood reports
if ThrEFFCI<10 then
    ThrEFFCISTR = "0" & string(ThrEFFCI)
else
    ThrEFFCISTR = string(ThrEFFCI)
end if

FileIN_ObsFF_costa = Git_repo & "/" & DirIN_ObsFF & "/" & "ObsFF_" & string(Year_ObsFF) & "_EFFCI" & string(ThrEFFCISTR) & "_Costa.csv"
ObsFF_costa = read_table(
    table_delimiter  : ",",
    table_header_row : 1,
    table_filename   : FileIN_ObsFF_costa
    )
ObsFF_Costa = create_geo(
                type      : 'xyv',
                latitudes : values(ObsFF_costa,2),
                longitudes: values(ObsFF_costa,1),
                values    : vector(count(values(ObsFF_costa,1)))+1
                )


FileIN_ObsFF_sierra = Git_repo & "/" & DirIN_ObsFF & "/ObsFF_" & string(Year_ObsFF) & "_EFFCI" & string(ThrEFFCISTR) & "_Sierra.csv"
ObsFF_sierra = read_table(
    table_delimiter  : ",",
    table_header_row : 1,
    table_filename   : FileIN_ObsFF_sierra
    )
ObsFF_Sierra = create_geo(
                type      : 'xyv',
                latitudes : values(ObsFF_sierra,2),
                longitudes: values(ObsFF_sierra,1),
                values    : vector(count(values(ObsFF_sierra,1)))+2
                )

FileIN_ObsFF_oriente = Git_repo & "/" & DirIN_ObsFF & "/" & "ObsFF_" & string(Year_ObsFF) & "_EFFCI" & string(ThrEFFCISTR) & "_Oriente.csv"
ObsFF_oriente = read_table(
    table_delimiter  : ",",
    table_header_row : 1,
    table_filename   : FileIN_ObsFF_oriente
    )
ObsFF_Oriente = create_geo(
                    type      : 'xyv',
                    latitudes : values(ObsFF_oriente,2),
                    longitudes: values(ObsFF_oriente,1),
                    values    : vector(count(values(ObsFF_oriente,1)))+3
                    )

ObsFF = ObsFF_Costa & ObsFF_Sierra & ObsFF_Oriente


# Plotting
coastlines = mcoast(
    map_coastline_resolution       : "full",
    map_coastline_colour           : "charcoal",
    map_coastline_thickness        : 3,
    map_coastline_sea_shade        : "on",
    map_coastline_sea_shade_colour : "RGB(0.7084,0.9465,0.9465)",
    map_boundaries                 : "on",
    map_boundaries_colour          : "blue",
    map_boundaries_thickness       : 3,
    map_grid                       : "off",
    map_label                      : "off"
    )

geo_view = geoview(
    map_area_definition : "corners",
    area                : [1.6,-81.2,-5.11,-75],
    coastlines          : coastlines
    )

contouring = mcont(
    contour                      : "off",
    contour_level_selection_type : "level_list",
    contour_level_list           : [0.1,1.1,2.1,3.1],
    contour_label                : "off",
    contour_shade                : "on",
    contour_shade_technique      : "grid_shading",
    contour_shade_colour_method  : "list",
    contour_shade_colour_list    : ["RGB(1.0000,0.8549,0.0000)","RGB(0.6000,0.3922,0.0000)","RGB(0.0000,0.5490,0.1882)"]
    )

symbol_black = msymb(
    legend              : "on",
    symbol_type         : "marker",
    symbol_table_mode   : "on",
    symbol_outline      : "on",
    symbol_min_table    : [0.1],
    symbol_max_table    : [3.1],
    symbol_colour_table : ["RGB(0000,0.0000,0.0000)"],
    symbol_marker_table : 15,
    symbol_height_table : 0.4
    )

symbol = msymb(
    legend              : "on",
    symbol_type         : "marker",
    symbol_table_mode   : "on",
    symbol_outline      : "on",
    symbol_min_table    : [0.1,1.1,2.1],
    symbol_max_table    : [1.1,2.1,3.1],
    symbol_colour_table : ["RGB(1.0000,0.8549,0.0000)", # 1 (La Costa)
                           "RGB(0.6000,0.3922,0.0000)", # 2 (La Sierra)
                           "RGB(0.0000,0.5490,0.1882)"], # 3 (El Oriente)
    symbol_marker_table : 15,
    symbol_height_table : 0.3
    )


# Saving the plot
FileOUT = Git_repo & "/" & DirOUT & "/EcuadorMask_ObsFF_" & string(Year_ObsFF) & "_EFFCI" & string(ThrEFFCISTR)
svg = svg_output(output_name: FileOUT)
setoutput(svg)
plot(geo_view, ecuador_mask, contouring, ObsFF, symbol_black, symbol, coastlines)